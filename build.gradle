/*
 * Copyright (c) 2025 cpmn.tech
 *
 * Licensed under the MIT License.
 * You may obtain a copy of the License at
 * https://opensource.org/licenses/MIT
 *
 * This file is part of the VulcanTestFramework project.
 * A QA Automation Project by Claudia Paola MuÃ±oz (cpmn.tech)
 */
  
plugins {
    id 'java'
}

group = 'com.vulcan'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


dependencies {
    // Base test framework (JUnit 4)
    testImplementation 'junit:junit:4.13.2'

    // Selenium WebDriver
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.20.0'

    // Cucumber JVM (Java + JUnit)
    testImplementation 'io.cucumber:cucumber-java:7.15.0'
    testImplementation 'io.cucumber:cucumber-junit:7.15.0'

    // WebDriverManager (automatic driver downloads)
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.7.0'

    // Logging - Log4j2
    testImplementation 'org.apache.logging.log4j:log4j-api:2.24.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.24.0'

    // API Testing - RestAssured
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    
}
 
def applyFrameworkSystemProps = {
    Test t ->
        // Map all UI + API keys from gradle.properties into System properties
        [
            'ui.baseUrl',
            'ui.browser',
            'ui.implicitWait',
            'api.baseUrl',
            'api.timeout',
            'env'
        ].each { key ->
            if (project.hasProperty(key)) {
                t.systemProperty key, project.property(key)
            }
        }
        // Show test results clearly in console
        t.testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            showStandardStreams = true
        }

        t.useJUnit()
        // Allow Gradle to succeed even if no tests are found   
        t.failOnNoDiscoveredTests = false

        // Forward cucumber tag filter from Gradle JVM -> Test JVM (so -D works)
        def tags = System.getProperty("cucumber.filter.tags")
        if (tags != null) {
            t.systemProperty("cucumber.filter.tags", tags)
        }
}

tasks.register('apiTest', Test) { Test t ->
    group = "verification"
    description = "Runs API Cucumber features only (features/api)."
    applyFrameworkSystemProps(t)

    // Ensure tests are compiled first
    t.dependsOn(tasks.named("testClasses"))

    // Make sure this task uses the test output + runtime classpath
    t.testClassesDirs = sourceSets.test.output.classesDirs
    t.classpath = sourceSets.test.runtimeClasspath

    // Run ONLY the runner class
    t.filter {
        includeTestsMatching("com.vulcan.framework.runners.CucumberTestRunner")
    }

    // Folder-based execution (NO tags required)
    t.systemProperty "cucumber.features", "src/test/resources/features/api"
}

tasks.register('uiTest', Test) { Test t ->
    group = "verification"
    description = "Runs UI Cucumber features only (features/ui)."
    applyFrameworkSystemProps(t)

    // Ensure tests are compiled first
    t.dependsOn(tasks.named("testClasses"))

    // Make sure this task uses the test output + runtime classpath
    t.testClassesDirs = sourceSets.test.output.classesDirs
    t.classpath = sourceSets.test.runtimeClasspath

    // Run ONLY the runner class
    t.filter {
        includeTestsMatching("com.vulcan.framework.runners.CucumberTestRunner")
    }

    // Folder-based execution (NO tags required)
    t.systemProperty "cucumber.features", "src/test/resources/features/ui"
}


tasks.register("allTests") {
    group = "verification"
    description = "Runs UI + API test suites."
    dependsOn("uiTest", "apiTest")
}

tasks.named('test', Test) { Test t ->

    // Ensure gradle.properties keys are available via System.getProperty(...)
    applyFrameworkSystemProps(t)
    // Default: discover all features (UI + API)
    t.systemProperty "cucumber.features", "src/test/resources/features"

    // Allow tag filtering from CLI
    // Example: -Dcucumber.filter.tags="@api"
}
